use std::str::FromStr;
use lalrpop_util::ErrorRecovery;

use crate::ast::*;

grammar<'err>(errors: &'err mut Vec<ErrorRecovery<usize, Token<'input>, &'static str>>);

pub Stat: Box<Expr> = {
    <id:ID> "=" <e:Expr> => Box::new(Expr::ID(ID::Name(id, e))),
    <e:Expr> => e,
};

Expr: Box<Expr> = {
    <e:Expr> <eo:ExprOp> <f:Fact> => Box::new(Expr::Op(e, eo, f)),
    <f: Func> => Box::new(Expr::Func(f)),
    Fact,
};

Fact: Box<Expr> = {
    <e:Fact> <eo:FactOp> <f:Term> => Box::new(Expr::Op(e, eo, f)),
    "(" <id:Func> <p:Params> ")" => Box::new(Expr::App(id, *p)),
    Term,
};

Func: Box<Func> = {
    <id:ID> => Box::new(Func::ID(ID::Name(id, Box::new(Expr::Error(Box::new(ErrorType::VariableNotBinded)))))),
    "(f" "(" <args:Args> ")" "=>" <e:Expr> ")" => Box::new(Func::Decl(*args, e)),
};

Params: Box<Params> = {
    <mut p:Params> <e:Expr> => { (*p).params.push(e); p },
    <e:Expr> => Box::new(Params { params: vec![e] }),
};

Args: Box<Args> = {
    <mut p:Args> "," <id:Variable>  => { (*p).args.push(id); p },
    <id:Var> => Box::new(Args { args: vec![ID::Name(id, Box::new(Expr::Error(Box::new(ErrorType::VariableNotBinded))))] }),
};

Variable: ID = {
    <var:Var> => ID::Name(var, Box::new(Expr::Error(Box::new(ErrorType::VariableNotBinded)))),
    ! => { errors.push(<>);
    ID::Error(Box::new(ErrorType::InnerVar), String::from("Inner variable must
    be lowercase")) },
}

Term: Box<Expr> = {
    <n:Num> => Box::new(Expr::Number(n)),
    <var:Var> => Box::new(Expr::ID(ID::Name(var, Box::new(Expr::Error(Box::new(ErrorType::VariableNotBinded)))))),
    "(" <e:Expr> ")" => e,
    ! => { errors.push(<>);
    Box::new(Expr::Error(Box::new(ErrorType::Unhandled))) },
};

ExprOp: Opcode = {
    "+" => Opcode::Add,
    "-" => Opcode::Sub,
};

FactOp: Opcode = {
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
};

Num: i32 = <s:r"[0-9]+"> => i32::from_str(s).unwrap();
ID: String = <s:r"[A-Z][_a-zA-Z]*"> => String::from_str(s).unwrap();
Var: String = <s:r"[a-z][_a-z]*"> => String::from_str(s).unwrap();
